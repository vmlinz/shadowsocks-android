buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:1.2.2"
        classpath "jp.leafytree.gradle:gradle-android-scala-plugin:1.4"
    }
}

repositories {
    jcenter()

    maven {
        url "http://madeye-maven-repository.googlecode.com/git/"
    }
}

apply plugin: "com.android.application"
apply plugin: "jp.leafytree.android-scala"

android {
    compileSdkVersion "android-22"
    buildToolsVersion "22.0.1"

    defaultConfig {
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "2g"
    }

    productFlavors {
        dev {
            minSdkVersion 21
        }

        prod {
            minSdkVersion 14
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/aidl', 'src/main/java']
            jniLibs.srcDirs = ['src/main/libs']
            jni.srcDirs = []
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile "org.scala-lang:scala-library:2.10.5"

    compile "com.android.support:multidex:1.0.1"
    androidTestCompile "com.android.support:multidex-instrumentation:1.0.1", { exclude module: "multidex" }
    compile "com.android.support:support-v4:22.1.1"
    compile "com.google.android.gms:play-services-base:7.3.0"
    compile "com.google.android.gms:play-services-ads:7.3.0"
    compile "com.google.zxing:android-integration:3.1.0"
    compile "dnsjava:dnsjava:2.1.7"
    compile "com.github.kevinsawicki:http-request:5.6"
    compile "commons-net:commons-net:3.3"
    compile "com.joanzapata.android:android-iconify:1.0.9"
    compile "net.glxn.qrgen:android:2.0"
    compile "net.simonvt.menudrawer:menudrawer:3.0.6"
    compile "com.github.mrengineer13:snackbar:0.5.0"
    compile "com.nostra13.universalimageloader:universal-image-loader:1.8.4"
    compile "com.j256.ormlite:ormlite-core:4.47"
    compile "com.j256.ormlite:ormlite-android:4.47"
    compile 'eu.chainfire:libsuperuser:1.0.0.+'
}

tasks.withType(ScalaCompile) {
    // If you want to use scala compile daemon
    scalaCompileOptions.useCompileDaemon = true
    // Suppress deprecation warnings
    scalaCompileOptions.deprecation = false
    // Additional parameters
    scalaCompileOptions.additionalParameters = ["-feature"]
}
